buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

ext.getGitCommitHash = {
    try {
        def gitFolder = "$projectDir/.git/"
        def takeFromHash = 7
        def head = new File(gitFolder + "HEAD").text.split(":")
        def isCommit = head.length == 1
        if(isCommit) return head[0].trim().take(takeFromHash)
        def refHead = new File(gitFolder + head[1].trim())
        refHead.text.trim().take takeFromHash
    } catch (Exception e) {
        "Unknown"
    }
}

version = projectVersion
ext.revision = getGitCommitHash()

apply plugin: 'java'
apply plugin: 'maven'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    jcenter()
}

defaultTasks 'clean', 'assemble'

dependencies {
    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs = ['.']
        }
    }
}

jar {
    version = revision
    manifest {
        attributes 'Specification-Title': 'JSON-java',
                   'Specification-Version': projectVersion,
                   'Specification-Vendor': 'com.github.stleary',
                   'Implementation-Title': 'JSON-java',
                   'Implementation-Version': version,
                   'Implementation-Vendor': 'com.github.letonez'
    }
}
